plugins {
    id 'net.kyori.indra.licenser.spotless' version '2.2.0' apply false
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

version '1.0.1-SNAPSHOT'
group = 'dev.vankka'

subprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'net.kyori.indra.licenser.spotless'

    sourceCompatability = JavaVersion.VERSION_1_8
    targetCompatability = JavaVersion.VERSION_1_8

    ext {
        dependencyDownloadVersion = '1.3.1'
    }

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()

        if (version.endsWith('-SNAPSHOT')) {
            maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
        }
    }

    java {
        withSourcesJar()
        withJavadocJar()
    }

    indraSpotlessLicenser {
        licenseHeaderFile(rootProject.file('buildscript/LICENSE_HEADER'))
        newLine(true)
        property('inception', '2021')
        property('year_now', String.valueOf(Calendar.getInstance().get(Calendar.YEAR)))
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifactId = 'minecraftdependencydownload-' + project.name
                pom {
                    name = 'minecraftdependencydownload-' + project.name
                    packaging = 'jar'
                    description = 'A library to download & load dependencies during runtime for Minecraft mods & plugins'
                    url = 'https://github.com/Vankka/MinecraftDependencyDownload'

                    scm {
                        connection = 'scm:git:https://github.com/Vankka/MinecraftDependencyDownload.git'
                        developerConnection = 'scm:git:https://github.com/Vankka/MinecraftDependencyDownload.git'
                        url = 'https://github.com/Vankka/MinecraftDependencyDownload'
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://www.opensource.org/licenses/mit-license.php'
                        }
                    }

                    developers {
                        developer {
                            id = 'Vankka'
                        }
                    }
                }
            }
        }
    }

    if (System.getenv('SIGNING_KEY') != null || project.hasProperty("signing.keyId")) {
        signing {
            sign publishing.publications.maven

            def key = System.getenv('SIGNING_KEY')
            if (key != null) {
                useInMemoryPgpKeys(key, System.getenv('SIGNING_KEY_PASS'))
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
            snapshotRepositoryUrl.set(uri('https://s01.oss.sonatype.org/content/repositories/snapshots/'))
            packageGroup = rootProject.group
        }
    }
}
